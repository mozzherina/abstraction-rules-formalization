%prefix( 
	: <http://example.com#>
	gufo: <https://purl.org/nemo/gufo#>
	rdfs: <http://www.w3.org/2000/01/rdf-schema#>
)%


logic OWL


ontology GUFO =
	<http://purl.org/nemo/gufo>
end

ontology RDFS = 
	<http://www.w3.org/2000/01/rdf-schema#>
end

ontology Base =
  	GUFO and RDFS
end


pattern Reified_Relation_0D [
	Class: D; ObjectProperty: f; Class: R; 
	ObjectProperty: role
] =

    	ObjectProperty: f Domain: D Range: R 
	ObjectProperty: f[role] SubPropertyOf: f

	Class: D SubClassOf: f[role] min 0
end


pattern NonSortal_Matching [
	Class: Endurant; Class: NonSortalType; 
	? ObjectProperty: relation; ? Class: EndurantOrDatatype
] given Base =

	Class: NonSortalType SubClassOf: gufo:NonSortal
	Class: Endurant 
		SubClassOf: gufo:EndurantType, NonSortalType
	Class: EndurantOrDatatype 
		SubClassOf: gufo:EndurantType or rdfs:Datatype

	ObjectProperty: relation 
		Domain: NonSortalType Range: EndurantOrDatatype 
end


pattern NonSortal_Replacing [
	Class: Endurant; Class: NonSortalType; 
	? ObjectProperty: relation; ? Class: EndurantOrDatatype
] given Base =

	Reified_Relation_0D[
		Endurant; relation; EndurantOrDatatype; 
		NonSortalType
	]
then		
	Class: Endurant SubClassOf: gufo:EndurantType
	Class: EndurantOrDatatype 
		SubClassOf: gufo:EndurantType or rdfs:Datatype

end


ontology CarOntology =
	Base
then 
	Class: Car SubClassOf: gufo:Kind
	Class: PhysicalObject SubClassOf: gufo:Mixin
	Class: Weight SubClassOf: rdfs:Datatype

	ObjectProperty: hasProperty 
		Domain: PhysicalObject Range: Weight
then	
	NonSortal_Matching [
		Car; PhysicalObject; 
		hasProperty; Weight
	]	
then %def	
	NonSortal_Replacing [
		Car; PhysicalObject; 
		hasProperty; Weight
	]
end